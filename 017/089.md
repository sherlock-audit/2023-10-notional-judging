Scrawny Velvet Goose

medium

# the value of `oneLPValueInPrimary` is over inflated

## Summary
`oneLPValueInPrimary` gotten from `_calculateLPTokenValue()` which is the value of one LP token in terms of the primary borrowed currency is over inflated as the amount is scaled to higher precision, this will lead to incorrect valuation of prices and amounts when converting, or getting exchange rates.
## Vulnerability Detail
`_calculateLPTokenValue()` is called by `_checkPriceAndCalculateValue` in the balancer and curve pools respectively, which is used in `getExchangeRate()`, `convertStrategyToUnderlying()`, and `reinvestReward()`.
you can see the code 
https://github.com/sherlock-audit/2023-10-notional/blob/main/leveraged-vaults/contracts/vaults/common/SingleSidedLPVaultBase.sol#L333C1-L367C6
```solidity
    function _calculateLPTokenValue(
        uint256[] memory balances,
@@@>        uint256[] memory spotPrices
    ) internal view returns (uint256 oneLPValueInPrimary) {
        (IERC20[] memory tokens, uint8[] memory decimals) = TOKENS();
        address primaryToken = address(tokens[PRIMARY_INDEX()]);
        uint256 primaryDecimals = 10 ** decimals[PRIMARY_INDEX()];
        uint256 totalSupply = _totalPoolSupply();
        uint256 limit = VaultStorage.getStrategyVaultSettings().oraclePriceDeviationLimitPercent;

        for (uint256 i; i < tokens.length; i++) {
            // Skip the pool token if it is in the token list (i.e. ComposablePools)
            if (address(tokens[i]) == address(POOL_TOKEN())) continue;
            // This is the claim on the pool balance of 1 LP token.
    @@@>        uint256 tokenClaim = balances[i] * POOL_PRECISION() / totalSupply;
            if (i == PRIMARY_INDEX()) {
                oneLPValueInPrimary += tokenClaim;
            } else {
                uint256 price = _getOraclePairPrice(primaryToken, address(tokens[i]));

                // Check that the spot price and the oracle price are near each other. If this is
                // not true then we assume that the LP pool is being manipulated.
                uint256 lowerLimit = price * (Constants.VAULT_PERCENT_BASIS - limit) / Constants.VAULT_PERCENT_BASIS;
                uint256 upperLimit = price * (Constants.VAULT_PERCENT_BASIS + limit) / Constants.VAULT_PERCENT_BASIS;
                if (spotPrices[i] < lowerLimit || upperLimit < spotPrices[i]) {
                    revert Errors.InvalidPrice(price, spotPrices[i]);
                }

                // Convert the token claim to primary using the oracle pair price.
                uint256 secondaryDecimals = 10 ** decimals[i];
    @@@>            oneLPValueInPrimary += (tokenClaim * POOL_PRECISION() * primaryDecimals) / 
                          (price * secondaryDecimals);
            }
        }
    }
```
This issue here is on calculation of `oneLPValueInPrimary` it multiplies  the values by the `POOL_PRECISION()` for scaling, however all the values have already be scaled before now, in the calculation of `tokenClaim` it scales it to the pool precision and the price which is calculated by `_getOraclePairPrice()` is already scaled to the pool's percision
https://github.com/sherlock-audit/2023-10-notional/blob/main/leveraged-vaults/contracts/vaults/common/SingleSidedLPVaultBase.sol#L322C1-L327C6
```soilidity
    function _getOraclePairPrice(address base, address quote) internal view returns (uint256) {
        (int256 rate, int256 precision) = TRADING_MODULE.getOraclePrice(base, quote);
        require(rate > 0);
        require(precision > 0);
@>        return uint256(rate) * POOL_PRECISION() / uint256(precision);
    }
```
**NOTE** the `spotPrices` have already be scaled before in  [`_checkPriceAndCalculateValue` ](https://github.com/sherlock-audit/2023-10-notional/blob/main/leveraged-vaults/contracts/vaults/BalancerWeightedAuraVault.sol#L88C1-L90C10) in the different pools, before the call to `_calculateLPTokenValue()` .
Therefore scaling again by the pool precision will over inflate the value of `oneLPValueInPrimary` to a higher value than it is supposed to be.
## Impact
where the LP value is over inflated it will lead to incorrect valuations of the price and amounts to convert to an underlying token in notional
## Code Snippet
- https://github.com/sherlock-audit/2023-10-notional/blob/main/leveraged-vaults/contracts/vaults/common/SingleSidedLPVaultBase.sol#L333C1-L367C6
- https://github.com/sherlock-audit/2023-10-notional/blob/main/leveraged-vaults/contracts/vaults/common/SingleSidedLPVaultBase.sol#L322C1-L327C6
- https://github.com/sherlock-audit/2023-10-notional/blob/main/leveraged-vaults/contracts/vaults/BalancerWeightedAuraVault.sol#L88C1-L90C10
## Tool used

Manual Review

## Recommendation
don't multiple the by `POOL_PRECISION()` when calculating `oneLPValueInPrimary`
```diff
    function _calculateLPTokenValue(
        uint256[] memory balances,
        uint256[] memory spotPrices
    ) internal view returns (uint256 oneLPValueInPrimary) {
        (IERC20[] memory tokens, uint8[] memory decimals) = TOKENS();
        address primaryToken = address(tokens[PRIMARY_INDEX()]);
        uint256 primaryDecimals = 10 ** decimals[PRIMARY_INDEX()];
        uint256 totalSupply = _totalPoolSupply();
        uint256 limit = VaultStorage.getStrategyVaultSettings().oraclePriceDeviationLimitPercent;

        for (uint256 i; i < tokens.length; i++) {
            // Skip the pool token if it is in the token list (i.e. ComposablePools)
            if (address(tokens[i]) == address(POOL_TOKEN())) continue;
            // This is the claim on the pool balance of 1 LP token.
            uint256 tokenClaim = balances[i] * POOL_PRECISION() / totalSupply;
            if (i == PRIMARY_INDEX()) {
                oneLPValueInPrimary += tokenClaim;
            } else {
                uint256 price = _getOraclePairPrice(primaryToken, address(tokens[i]));

                // Check that the spot price and the oracle price are near each other. If this is
                // not true then we assume that the LP pool is being manipulated.
                uint256 lowerLimit = price * (Constants.VAULT_PERCENT_BASIS - limit) / Constants.VAULT_PERCENT_BASIS;
                uint256 upperLimit = price * (Constants.VAULT_PERCENT_BASIS + limit) / Constants.VAULT_PERCENT_BASIS;
                if (spotPrices[i] < lowerLimit || upperLimit < spotPrices[i]) {
                    revert Errors.InvalidPrice(price, spotPrices[i]);
                }

                // Convert the token claim to primary using the oracle pair price.
-               uint256 secondaryDecimals = 10 ** decimals[i];
-               oneLPValueInPrimary += (tokenClaim * POOL_PRECISION() * primaryDecimals) / 
-                    (price * secondaryDecimals);
+                uint256 secondaryDecimals = 10 ** decimals[i];
+                oneLPValueInPrimary += (tokenClaim * primaryDecimals) / 
+                    (price * secondaryDecimals);
            }
        }
    }
```