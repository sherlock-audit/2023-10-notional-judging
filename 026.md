Scrawny Velvet Goose

high

# `_deleverageVaultAccount()` in `FlashLiquidatorBase.sol` is prone to read-only reentrancy whenever `actionParams.useVaultDeleverage` is false allowing for reentrancy

## Summary
`_deleverageVaultAccount()` in `FlashLiquidatorBase.sol` is used when handling liquidations,  the issue is that when `actionParams.useVaultDeleverage`  is equal to false is doesn't check against read only reentrancy which will allow for reentrancy on balancer and curve pools.
## Vulnerability Detail
Let me start by saying the devs are well aware of this issue(read-only reentrancy in balancer and curve pools) as you can see in you can see `_deleverageVaultAccount()` 
https://github.com/sherlock-audit/2023-10-notional/blob/main/leveraged-vaults/contracts/utils/FlashLiquidatorBase.sol#L136C1-L175C6
```solidity
    function _deleverageVaultAccount(LiquidationParams memory params) private {
        (uint16 currencyIndex, int256 maxUnderlying) = _getOptimalDeleveragingParams(params.account, params.vault);
        require(maxUnderlying > 0);


        DeleverageVaultAccountParams memory actionParams = abi.decode(params.actionData, (DeleverageVaultAccountParams));
        uint256 vaultSharesFromLiquidation;
        if (actionParams.useVaultDeleverage) {
            (
                vaultSharesFromLiquidation, /* */ 
            ) = IStrategyVault(params.vault).deleverageAccount{value: address(this).balance}(
                params.account, 
                params.vault, 
                address(this), 
                currencyIndex,
                maxUnderlying
            );
        } else {
            (
                vaultSharesFromLiquidation, /* */ 
            ) = NOTIONAL.deleverageAccount{value: address(this).balance}(
                params.account, 
                params.vault, 
                address(this), 
                currencyIndex,
                maxUnderlying
            );
        }


        if (0 < vaultSharesFromLiquidation) {
            NOTIONAL.exitVault(
                address(this), 
                params.vault, 
                address(this), 
                vaultSharesFromLiquidation,
                0, 
                0, 
                actionParams.redeemData
            );
        }
    }
```
when `actionParams.useVaultDeleverage = true` the call to `IStrategyVault(params.vault).deleverageAccount` is shown as
https://github.com/sherlock-audit/2023-10-notional/blob/main/leveraged-vaults/contracts/vaults/common/BaseStrategyVault.sol#L217C1-L229C6
```solidity
    function deleverageAccount(
        address account,
        address vault,
        address liquidator,
        uint16 currencyIndex,
        int256 depositUnderlyingInternal
    ) external payable returns (uint256 vaultSharesFromLiquidation, int256 depositAmountPrimeCash) {
        require(msg.sender == liquidator);
        _checkReentrancyContext();
        return NOTIONAL.deleverageAccount{value: msg.value}(
            account, vault, liquidator, currencyIndex, depositUnderlyingInternal
        );
    }
```
You can see it checks against reentrancy on the call to  `_checkReentrancyContext();` before `NOTIONAL.deleverageAccount()` is called.
- for balancer pools
https://github.com/sherlock-audit/2023-10-notional/blob/main/leveraged-vaults/contracts/vaults/balancer/mixins/BalancerPoolMixin.sol#L131C3-L136C6
```solidity

    /// @notice the re-entrancy context is checked during liquidation to prevent read-only
    /// reentrancy on all balancer pools.
    function _checkReentrancyContext() internal override {
        IBalancerVault.UserBalanceOp[] memory noop = new IBalancerVault.UserBalanceOp[](0);
        Deployments.BALANCER_VAULT.manageUserBalance(noop);
    }
```
- for curve pools
https://github.com/sherlock-audit/2023-10-notional/blob/main/leveraged-vaults/contracts/vaults/curve/Curve2TokenPoolMixin.sol#L101C1-L106C6
```solidity
    function _checkReentrancyContext() internal override {
        // We need to set the LP token amount to 1 for Curve V2 pools to bypass
        // the underflow check
        uint256[2] memory minAmounts;
        ICurve2TokenPool(address(CURVE_POOL)).remove_liquidity(IS_CURVE_V2 ? 1 : 0, minAmounts);
    }
```
However whenever  `actionParams.useVaultDeleverage = False`  the call to `_deleverageVaultAccount()`  is skipped and it calls `NOTIONAL.deleverageAccount()` directly without checking against the reentrancy that can happen during liquidations.
## Impact
whenever `actionParams.useVaultDeleverage = False`  liquidations are prone to reentrancy allowing exploits on balancer and curve pools.
## Code Snippet
- https://github.com/sherlock-audit/2023-10-notional/blob/main/leveraged-vaults/contracts/utils/FlashLiquidatorBase.sol#L136C1-L175C6
- https://github.com/sherlock-audit/2023-10-notional/blob/main/leveraged-vaults/contracts/vaults/common/BaseStrategyVault.sol#L217C1-L229C6
## Tool used

Manual Review, curve pool exploit

## Recommendation
depending on the pool the liquidation happens in`(curve or balancer)` the corresponding `_checkReentrancyContext()` should be called before the call to `NOTIONAL.deleverageAccount()` 
```diff
    function _deleverageVaultAccount(LiquidationParams memory params) private {
        (uint16 currencyIndex, int256 maxUnderlying) = _getOptimalDeleveragingParams(params.account, params.vault);
        require(maxUnderlying > 0);


        DeleverageVaultAccountParams memory actionParams = abi.decode(params.actionData, (DeleverageVaultAccountParams));
        uint256 vaultSharesFromLiquidation;
        if (actionParams.useVaultDeleverage) {
            (
                vaultSharesFromLiquidation, /* */ 
            ) = IStrategyVault(params.vault).deleverageAccount{value: address(this).balance}(
                params.account, 
                params.vault, 
                address(this), 
                currencyIndex,
                maxUnderlying
            );
        } else {
+    _checkReentrancyContext();
            (
                vaultSharesFromLiquidation, /* */ 
            ) = NOTIONAL.deleverageAccount{value: address(this).balance}(
                params.account, 
                params.vault, 
                address(this), 
                currencyIndex,
                maxUnderlying
            );
        }


        if (0 < vaultSharesFromLiquidation) {
            NOTIONAL.exitVault(
                address(this), 
                params.vault, 
                address(this), 
                vaultSharesFromLiquidation,
                0, 
                0, 
                actionParams.redeemData
            );
        }
    }
```